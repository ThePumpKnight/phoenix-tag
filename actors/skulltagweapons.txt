//=================================================================================================
//
// Minigun
//
//=================================================================================================

ACTOR PTag_Minigun : DoomWeapon 5014
{
	SpawnID 214
	Radius 20
	Height 16
	Weapon.AmmoType "Clip" 
	Weapon.AmmoGive 20
	Weapon.AmmoUse 1
	Weapon.Selectionorder 700
	Weapon.SisterWeapon SpreadMinigun
	Weapon.SlotNumber 4 // This line isn't in skulltag.pk3, which instead defines the slot directly in DoomPlayer
	Tag "$TAG_PTAG_MINIGUN"
	Inventory.PickupMessage "$PICKUP_MINIGUN" // "You got the minigun!"
	Obituary "$OB_MINIGUN" // "%o was drilled by %k's minigun."
	AttackSound "weapons/minigun"
	States
	{
	Spawn:
		MNGN A -1
		Loop
	Ready:
		MNGG A 1 A_WeaponReady
		Loop
	Deselect:
		MNGG A 1 A_Lower
		Loop
	Select: 
		MNGG A 1 A_Raise 
		Loop	
	Fire: 
		MNGG A 0 A_GunFlash
		MNGG A 2 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
		MNGG B 2 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
		MNGG A 2 A_ReFire
		MNGG B 2
		MNGG A 4
		MNGG B 4
		MNGG A 8
		MNGG B 8
		Goto Ready 
	Flash: 
		MNGF A 3 BRIGHT A_Light1 
		MNGF B 3 BRIGHT A_Light0
		Stop 
	}
}

//=================================================================================================
//
// RailGun
//
//=================================================================================================

ACTOR PTag_Railgun : DoomWeapon 5012
{
	SpawnID 164
	Radius 20
	Height 16
	Weapon.Selectionorder 100
	Weapon.AmmoUse 10
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Weapon.SisterWeapon SpreadRailgun
	Weapon.SlotNumber 6 // This line isn't in skulltag.pk3, which instead defines the slot directly in DoomPlayer
	Tag "$TAG_PTAG_RAILGUN"
	Inventory.Pickupmessage "$PICKUP_RAILGUN" // "You got the railgun!"
	Obituary "$OB_RAILGUN" // "%o was railed by %k."
	States
	{
	Ready:
		RLGG A 1 A_WeaponReady
		Loop
	Deselect:
		RLGG A 1 A_Lower
		Loop
	Select:
		RLGG A 1 A_Raise
		Loop
	Fire:
		RLGG E 12 A_FireRailgun
		RLGG F 6 A_CheckForReload(4, "Reloaded")
		RLGG GHIJK 6
		RLGG L 6 A_ResetReloadCounter
	Reloaded:
		RLGG A 6
		RLGG M 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 5 bright A_Light1
		TNT1 A 5 bright A_Light2
		TNT1 A 0 bright A_Light0
		Goto LightDone
	Spawn:
		RAIL A -1
		Stop
	}
}

//=================================================================================================
//
// [TP] This allows the railgun to pierce armor
//
//=================================================================================================
ACTOR PTag_RailgunPuff : BulletPuff
{
	DamageType Railgun
	+PIERCEARMOR
	States
	{
	Spawn:
	Melee:
		TNT1 A 1
		stop
	}
}

//=================================================================================================
//
// BFG10K
//
//=================================================================================================

ACTOR PTag_BFG10K : DoomWeapon 5013
{
	SpawnID 165
	Radius 20
	Height 20
	Weapon.Selectionorder 2800
	Weapon.AmmoUse 5
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Weapon.SisterWeapon SpreadBFG10K
	Weapon.SlotNumber 7 // This line isn't in skulltag.pk3, which instead defines the slot directly in DoomPlayer
	Tag "$TAG_PTAG_BFG10K"
	Inventory.Pickupmessage "$PICKUP_BFG10K" // "You got the BFG10000!	Hell, yes!"
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOLMS
	States
	{
	Spawn:
		BFG2 A -1
		Stop
	Ready:
		BG2G A 0 A_PlaySound ("weapons/bfg10kidle")
		BG2G A 1 A_WeaponReady
		BG2G A 1 A_WeaponReady
		BG2G A 1 A_WeaponReady
		BG2G B 1 A_WeaponReady
		BG2G B 1 A_WeaponReady
		BG2G B 1 A_WeaponReady
		BG2G C 1 A_WeaponReady
		BG2G C 1 A_WeaponReady
		BG2G C 1 A_WeaponReady
		BG2G D 1 A_WeaponReady
		BG2G D 1 A_WeaponReady
		BG2G D 1 A_WeaponReady
		Loop
	Deselect:
		BG2G E 1 A_Lower
		Loop
	Select:
		BG2G E 1 A_Raise
		Loop
	Fire:
		BG2G E 20 A_PlaySound ("weapons/bfg10kf")
		BG2G F 4
		BG2G G 1
		BG2G H 1
		BG2G I 1
		BG2G J 1
		Goto Hold
	Hold:
		BG2G K 2 A_GunFlash
		BG2G L 2 A_FireBullets(0,0,1,0,"PTag_BFG10kShot")
		BG2G M 2
		BG2G N 0 A_ReFire
		BG2G O 35 A_PlaySound ("weapons/bfg10kcool")
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 3 Bright
		Goto LightDone
		Stop
	}
}

//=================================================================================================
//
// BFG10K explosive shot
//
//=================================================================================================

ACTOR PTag_BFG10kShot
{
	SpawnID 217
	Radius 11
	Height 8
	Speed 20
	Damage 160
	DamageType BFG10k
	+NOBLOCKMAP
	+NOGRAVITY
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+NOTELEPORT 
	+PUFFONACTORS
	+PUFFGETSOWNER
	+FORCERADIUSDMG
	Renderstyle Add
	Alpha 0.75
	SeeSound "weapons/bfg10kx"
	AttackSound "weapons/bfg10kx"
	Obituary "$OB_BFG10K" // "%o was blasted by %k's BFG10K."
	States
	{
	Spawn:
		BFE1 A 0 Bright
		BFE1 A 3 Bright A_Detonate
		BFE1 BCDEF 3 Bright
		Stop
	}
}

//=================================================================================================
//
// GrenadeLauncher
//
//=================================================================================================

ACTOR PTag_GrenadeLauncher : DoomWeapon 5011
{
	SpawnID 163
	Radius 20
	Height 16
	Weapon.Selectionorder 2500
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	Weapon.SisterWeapon SpreadGrenadeLauncher
	Weapon.SlotNumber 5 // This line isn't in skulltag.pk3, which instead defines the slot directly in DoomPlayer
	Tag "$TAG_PTAG_GRENADELAUNCHER"
	Inventory.PickupMessage "$PICKUP_GRENADELAUNCHER" // "You got the grenade launcher!"

	// action native A_FireSTGrenade ();

	States
	{
	Spawn:
		GLAU A -1
		Stop
	Ready:
		GRLG A 1 A_WeaponReady
		Loop
	Deselect:
		GRLG A 1 A_Lower
		Loop
	Select: 
		GRLG A 1 A_Raise
		Loop
	Fire: 
		GRLG B 8 A_GunFlash
		GRLG B 12 A_FireSTGrenade
		GRLG B 0 A_ReFire
		Goto Ready 
	Flash: 
		GRLF A 3 bright A_Light1
		GRLF B 4 bright
		GRLF C 4 bright A_Light2
		GRLF D 4 bright A_Light2
		Goto LightDone
	}
}

//=================================================================================================
//
// SPREAD WEAPONS // Thanks again, Remmirath! o7
//
//=================================================================================================

ACTOR NewRocketLauncher : RocketLauncher 2003
{
	Weapon.SlotNumber 5
	Weapon.SlotPriority 0
	Weapon.SisterWeapon RocketLauncherSpread
}

ACTOR RocketLauncherSpread : NewRocketLauncher
{
	+POWERED_UP
	Weapon.SisterWeapon NewRocketLauncher
	States
	{
	Fire:
		MISG B 8 A_GunFlash
		TNT1 A 0 A_FireProjectile("Rocket",0,1,0,0,0)
		TNT1 A 0 A_FireProjectile("Rocket",15,0,0,0,0)
		TNT1 A 0 A_FireProjectile("Rocket",-15,0,0,0,0)
		MISG B 12
		MISG B 0 A_ReFire
		Goto Ready
	Flash:
		MISF A 3 Bright A_Light1
		MISF B 4 Bright
		MISF CD 4 Bright A_Light2
		Goto LightDone
	}
}

ACTOR NewSuperShotgun : SuperShotgun 82
{
	Weapon.SlotNumber 3
	Weapon.SlotPriority 1
	Weapon.SisterWeapon SuperShotgunSpread
	Decal Bulletchip
}

ACTOR SuperShotgunSpread : NewSuperShotgun
{
	+POWERED_UP
	Weapon.SisterWeapon NewSuperShotgun
	States
	{
	Fire:
		SHT2 A 3
		SHT2 A 0 A_FireBullets (11.2, 7.1, 20, 5, "BulletPuff")
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_FireBullets (-15+frandom(-11.2,11.2), frandom(-7.1,7.1), -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_FireBullets (15+frandom(-11.2,11.2), frandom(-7.1,7.1), -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		SHT2 A 0 A_PlaySound ("weapons/sshotf", CHAN_WEAPON)
		SHT2 A 7 A_GunFlash
		SHT2 B 7
		SHT2 C 7 A_CheckReload
		SHT2 D 7 A_OpenShotgun2
		SHT2 E 7
		SHT2 F 7 A_LoadShotgun2
		SHT2 G 6
		SHT2 H 6 A_CloseShotgun2
		SHT2 A 5 A_ReFire
		Goto Ready
	// unused states
		SHT2 B 7
		SHT2 A 3
		Goto Deselect
	Flash:
		SHT2 I 4 Bright A_Light1
		SHT2 J 3 Bright A_Light2
		Goto LightDone
	}
}

ACTOR NewShotgun : Shotgun 2001
{
	Weapon.SlotNumber 3
	Weapon.SlotPriority 0
	Weapon.SisterWeapon SpreadShotgun
	Decal Bulletchip
}

ACTOR SpreadShotgun : NewShotgun
{
	+POWERED_UP
	Weapon.SisterWeapon NewShotgun
	States
	{
	Fire:
		SHTG A 3
		SHTG A 0 A_FireBullets (5.6, 0, 7, 5, "BulletPuff")
		TNT1 AAAAAAA 0 A_FireBullets (-15+frandom(-5.6,5.6), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 AAAAAAA 0 A_FireBullets (15+frandom(-5.6,5.6), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		SHTG A 0 A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
		SHTG A 7 A_GunFlash
		SHTG BC 5
		SHTG D 4
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_ReFire
		Goto Ready
	Flash:
		SHTF A 4 Bright A_Light1
		SHTF B 3 Bright A_Light2
		Goto LightDone
	}
}

ACTOR NewChaingun : Chaingun 2002
{
	Weapon.SlotNumber 4
	Weapon.SlotPriority 0
	Weapon.SisterWeapon SpreadChaingun
	Decal Bulletchip
}

ACTOR SpreadChaingun : NewChaingun
{
	+POWERED_UP
	Weapon.SisterWeapon NewChaingun
	States
	{
	Fire:
		CHGG A 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
		TNT1 A 0 A_FireBullets (15, 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (-15, 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (0, 0, -1, 5, "BulletPuff")
		CHGG A 0 A_GunFlash
		CHGG A 4 
		CHGG B 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
		TNT1 A 0 A_FireBullets (15, 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (-15, 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (0, 0, -1, 5, "BulletPuff")
		CHGG B 0 A_GunFlash("Flash2")
		CHGG B 4
		CHGG B 0 A_ReFire
		Goto Ready
	Hold:
		CHGG A 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
		TNT1 A 0 A_FireBullets (15+frandom(-5.6,5.6), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (-15+frandom(-5.6,5.6), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (5.6, 0, -1, 5, "BulletPuff")
		CHGG A 0 A_GunFlash
		CHGG A 4
		CHGG B 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
		TNT1 A 0 A_FireBullets (15+frandom(-5.6,5.6), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (-15+frandom(-5.6,5.6), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (5.6, 0, -1, 5, "BulletPuff")
		CHGG B 0 A_GunFlash("Flash2")
		CHGG B 4
		CHGG B 0 A_ReFire
		Goto Ready
	Flash:
		CHGF A 5 Bright A_Light1
		Goto LightDone
	Flash2:
		CHGF B 5 Bright A_Light2
		Goto LightDone
	}
}

ACTOR NewPistol : Pistol replaces Pistol 5010
{
	Weapon.SlotNumber 2
	Weapon.SisterWeapon SpreadPistol
	Decal Bulletchip
}

Actor SpreadPistol : NewPistol
{
	+POWERED_UP
	Weapon.SisterWeapon NewPistol
	States
	{
	Fire:
		PISG A 4
		PISG B 0 A_FireBullets (0, 0, -1, 5, "BulletPuff")
		PISG B 0 A_FireBullets (15, 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		PISG B 0 A_FireBullets (-15, 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		PISG B 0 A_PlaySound("weapons/pistol", CHAN_WEAPON)
		PISG B 6 A_GunFlash
		PISG C 4
		PISG B 5 A_ReFire
		Goto Ready
	Hold:
		PISG A 4
		PISG B 0 A_FireBullets (5.6, 0, -1, 5, "BulletPuff")
		PISG B 0 A_FireBullets (15+frandom(-5.6,5.6), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		PISG B 0 A_FireBullets (-15+frandom(-5.6,5.6), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		PISG B 0 A_PlaySound("weapons/pistol", CHAN_WEAPON)
		PISG B 6 A_GunFlash
		PISG C 4
		PISG B 5 A_ReFire
		Goto Ready
	Flash:
		PISF A 7 Bright A_Light1
		Goto LightDone
		PISF A 7 Bright A_Light1
		Goto LightDone
	}
}

Actor SpreadRailgun : PTag_Railgun
{
  +POWERED_UP
  Weapon.SisterWeapon PTag_Railgun
  States
  {
  Fire:
	RLGG E 0 A_RailAttack(150, 0, 0, "00 80 ff", "ff ff ff", RGF_EXPLICITANGLE,0,"none",15,0)
	RLGG E 0 A_RailAttack(150, 0, 0, "00 80 FF", "ff ff ff", RGF_EXPLICITANGLE,0,"none",-15,0)
	RLGG E 0 A_RailAttack(150, 0, 1, "00 80 FF", "ff ff ff", RGF_EXPLICITANGLE,0,"none",0,0)
	RLGG E 12
    RLGG F 6 A_CheckForReload(4, "Reloaded")
    RLGG GHIJK 6
    RLGG L 6 A_ResetReloadCounter
  Reloaded:
    RLGG A 6
    RLGG M 0 A_ReFire
    Goto Ready
  Flash:
    TNT1 A 5 bright A_Light1
    TNT1 A 5 bright A_Light2
    TNT1 A 0 bright A_Light0
    Goto LightDone
  }
}

Actor NewPlasmaRifle : PlasmaRifle 2004
{
	Weapon.SlotNumber 6
	Weapon.SlotPriority 0
	Weapon.SisterWeapon SpreadPlasmaRifle
}

Actor SpreadPlasmaRifle: NewPlasmaRifle
{
	+POWERED_UP
	Weapon.SisterWeapon NewPlasmaRifle
	States
	{
	Fire:
		TNT1 A 0 A_FireProjectile("PlasmaBallSpread",15,0,0,0,0)
		TNT1 A 0 A_FireProjectile("PlasmaBallSpread",-15,0,0,0,0)
		PLSG A 3 A_FirePlasma
		PLSG B 20 A_ReFire
		Goto Ready
	Flash:
		PLSF A 4 Bright A_Light1
		Goto LightDone
		PLSF B 4 Bright A_Light1
		Goto LightDone
	}
}

ACTOR PlasmaBallSpread
{
	Game Doom
	SpawnID 51
	Radius 13
	Height 8
	Speed 25
	Damage 5
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.75
	DeathSound "weapons/plasmax"
	Obituary "$OB_MPPLASMARIFLE"
	Decal Plasmascorch
	States
	{
	Spawn:
		PLSS AB 6 Bright
		Loop
	Death:
		PLSE ABCDE 4 Bright
		Stop
	}
}

Actor NewBFG9000 : BFG9000 2006
{
	Weapon.SlotNumber 7
	Weapon.SisterWeapon SpreadBFG9000
	Weapon.SlotPriority 0
}

Actor SpreadBFG9000 : NewBFG9000
{
	+POWERED_UP
	Weapon.SisterWeapon NewBFG9000
	States
	{
	Fire:
		BFGG A 20 A_BFGsound
		BFGG B 10 A_GunFlash
		TNT1 A 0 A_FireProjectile("BFGBall",15,0,0,0,0)
		TNT1 A 0 A_FireProjectile("BFGBall",-15,0,0,0,0)
		BFGG B 10 A_FireBFG
		BFGG B 20 A_ReFire
		Goto Ready
	Flash:
		BFGF A 11 Bright A_Light1
		BFGF B 6 Bright A_Light2
		Goto LightDone
	}
}

Actor SpreadGrenadeLauncher : PTag_GrenadeLauncher
{
	+POWERED_UP
	Weapon.SisterWeapon PTag_GrenadeLauncher
	States
	{
	Fire: 
		GRLG B 8 A_GunFlash
		TNT1 A 0 A_FireCustomMissile("PTag_Grenade", 15, 0, 0, 0, 0, 6.328125)
		TNT1 A 0 A_FireCustomMissile("PTag_Grenade", -15, 0, 0, 0, 0, 6.328125)
		GRLG B 12 A_FireCustomMissile("PTag_Grenade", 0, 1, 0, 0, 0, 6.328125)
		GRLG B 0 A_ReFire
		Goto Ready 
	Flash: 
		GRLF A 3 Bright A_Light1
		GRLF B 4 Bright
		GRLF C 4 Bright A_Light2
		GRLF D 4 Bright A_Light2
		Goto LightDone
	}
}

Actor SpreadMinigun : PTag_Minigun
{
	+POWERED_UP
	Weapon.SisterWeapon PTag_Minigun
	States
	{
	Fire:
		MNGG A 0 A_GunFlash
		TNT1 A 0 A_FireBullets (15, 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (-15, 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		MNGG A 2 A_FireBullets(0, 1, -1, 5)
		TNT1 A 0 A_FireBullets (15, 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (-15, 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		MNGG B 2 A_FireBullets(0, 1, -1, 5)
		MNGG A 2 A_ReFire
		MNGG B 2
		MNGG A 4
		MNGG B 4
		MNGG A 8
		MNGG B 8
		goto Ready
	Hold:
		MNGG A 0 A_GunFlash
		TNT1 A 0 A_FireBullets (15+frandom(-5,5), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (-15+frandom(-5,5), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		MNGG A 2 A_FireBullets(5, 1, -1, 5)
		TNT1 A 0 A_FireBullets (15+frandom(-5,5), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (-15+frandom(-5,5), 0, -1, 5, "BulletPuff", FBF_EXPLICITANGLE)
		MNGG B 2 A_FireBullets(5, 1, -1, 5)
		MNGG A 2 A_ReFire
		MNGG B 2
		MNGG A 4
		MNGG B 4
		MNGG A 8
		MNGG B 8
		goto Ready 
	Flash:
		MNGF A 3 BRIGHT A_Light1
		MNGF B 3 BRIGHT A_Light0
		Stop
	}
}

Actor SpreadBFG10K : PTag_BFG10K
{
	+POWERED_UP
	Weapon.SisterWeapon PTag_BFG10K
	States
	{
	Fire:
		BG2G E 20 A_PlaySound("Weapons/BFG10KF")
		BG2G F 4
		BG2G G 1
		BG2G H 1
		BG2G I 1
		BG2G J 1
		Goto Hold
	Hold:
		BG2G K 2 A_GunFlash
		TNT1 A 0 A_FireBullets (15, 0, -1, 0, "PTag_BFG10KShot", FBF_EXPLICITANGLE)
		TNT1 A 0 A_FireBullets (-15, 0, -1, 0, "PTag_BFG10KShot", FBF_EXPLICITANGLE)
		BG2G L 2 A_FireBullets(0, 0, 1, 0, "PTag_BFG10kShot")
		BG2G M 2
		BG2G N 0 A_ReFire
		BG2G O 35 A_PlaySound("Weapons/BFG10KCool")
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 3 bright
		Goto LightDone
	}
}

Actor NewChainsaw : Chainsaw 2005
{
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Weapon.SisterWeapon SpreadChainsaw
	Decal Bulletchip
}

Actor SpreadChainsaw : NewChainsaw
{
	+POWERED_UP
	Weapon.SisterWeapon NewChainsaw
	States
	{
		Fire:
		TNT1 A 0 A_Saw("","",0,"BulletPuff",SF_NOUSEAMMO,65,25.3125,0)
		TNT1 A 0 A_Saw("","",0,"BulletPuff",SF_NOUSEAMMO,65,25.3125,0)
		SAWG A 4 A_Saw
		TNT1 A 0 A_Saw("","",0,"BulletPuff",SF_NOUSEAMMO,65,25.3125,0)
		TNT1 A 0 A_Saw("","",0,"BulletPuff",SF_NOUSEAMMO,65,25.3125,0)
		SAWG B 4 A_Saw
		SAWG B 0 A_ReFire
		Goto Ready
	}
}

Actor NewFist: Fist replaces Fist
{
	Weapon.SlotNumber 1
	Weapon.SlotPriority 0
	Weapon.SisterWeapon SpreadFist
}

Actor SpreadFist: Newfist
{
	+POWERED_UP
	Weapon.SisterWeapon NewFist
	States
	{
	Fire:
		PUNG B 4
		TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "Berserked")
	Normal:
		TNT1 A 0 A_FireProjectile("FistAttack0",15,0,0,0,0)
		TNT1 A 0 A_FireProjectile("FistAttack0",-15,0,0,0,0)
		PUNG C 4 A_CustomPunch(2 * random(1, 10), TRUE)
		Goto FireEnd
	Berserked:
		TNT1 A 0 A_FireProjectile("BerserkAttack0",15,0,0,0,0)
		TNT1 A 0 A_FireProjectile("BerserkAttack0",-15,0,0,0,0)
		PUNG C 4 A_CustomPunch(20 * random(1, 10), TRUE)
	FireEnd:
		PUNG D 5
		PUNG C 4
		PUNG B 5 A_ReFire
		Goto Ready
	}
}

ACTOR FistAttack0 : FastProjectile
{
    Radius 1
    Height 1
    Speed 64
    Damage (2*random(1,10))
    Projectile
    +BLOODSPLATTER
    DamageType "HoloDoom0"
    States
    {
    Spawn:
        TNT1 A 1
        Stop
    XDeath: // When hitting a bleeding actor.
        TNT1 A 0 A_PlaySound("*fist")
        stop
    Crash: // When hitting a +NOBLOOD actor.
        TNT1 A 0 A_PlaySound("*fist")
        TNT1 A 0 A_SpawnItem("BulletPuff2")
        stop
    Death: // When hitting a wall.
        TNT1 A 0 A_SpawnItem("BulletPuff2")
        stop
    }
}

ACTOR BulletPuff2 : BulletPuff
{
+ALLOWPARTICLES
States
{
Spawn:
PUFF CD 4
Stop
}
}

ACTOR BerserkAttack0 : FistAttack0 {Damage (20*random(1,10))}

ACTOR NewBerserk : Berserk replaces Berserk 2023
{
  States
  {
  Pickup:
    TNT1 A 0 A_GiveInventory("PowerStrength")
    TNT1 A 0 HealThing(100, 0)
    TNT1 A 0 A_SelectWeapon("NewFist")
    Stop
  }
}

Actor PTag_Grenade
{
  Radius 8
  Height 8
  Speed 25
  Damage 20
  Gravity 0.25
  Projectile
  -NoGravity
  +DoomBounce
  +DehExplosion
  +GrenadeTrail
  +ExplodeOnDeath
  SeeSound "Weapons/GrBnce"
  DeathSound "Weapons/GrenLX"
  DamageType Fire
  States
  {
  Spawn:
    SGRN A 0 Bright
    SGRN A 0 Bright A_StopSound
    SGRN A 0 Bright A_PlaySound("Weapons/GrenLF")
    SGRN A 1 Bright 
    Goto Spawn+3
  Death:
    MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
    MISL B 8 Bright A_Explode
    MISL C 6 Bright
    MISL D 4 Bright
    stop
  }
}